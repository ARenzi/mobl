module mobl::ui::generic

import mobl

load mobl/ui/generic.css
load mobl/ui/generic/css-beziers.js
load mobl/ui/generic/touchscroll.js
load mobl/ui/generic/touchscroll.css

resource mobl/ui/generic/arrow.png
resource mobl/ui/generic/contextmenu.png

control header(text : String, onclick : Callback = null) {
  <div class="header" onclick=onclick>
    <div class="headerContainer"><div databind=text class="headerText"/></div>
    elements()
  </div>
}

control button(text : String, onclick : Callback = null) {
  <span class="button" onclick=onclick databind=text/>
}

control sideButton(text : String, onclick : Callback = null) {
  <span class="sideButton" onclick=onclick databind=text/>
}

control backButton(text : String, onclick : Callback = null) {
  <span class="backButton" onclick=onclick databind=text/>
}

control group() {
  <ul class="group">
    elements()
  </ul>
}

control item(onclick : Callback = null) {
  <li class="item" onclick=onclick>
    elements()
  </li>
}

control itemArrow(onclick : Callback = null) {
  <li class="itemArrow" onclick=onclick>
    elements()
  </li>
}

control checkBox(b : Bool, label : String = "") {
  <input type="checkbox" databind=b/>
  " "
  <span databind=label onclick={ b = b ? false : true; }/>
}

control textField(s : String, placeholder : String = null, onchange : Callback = null) {
  <input type="text" placeholder=placeholder databind=s onchange=onchange onblur={ scrollUp(); }/>
}

control numField(n : Num, placeholder : String = null, onchange : Callback = null) {
  <input type="text" placeholder=placeholder databind=n onchange=onchange />
}

control float(side : String = "right", distance : Num = 0) {
  <span class="float" style="float: " + side + "; " + (side == "left" ? "margin-left: " + distance + "px;" : "margin-right: " + distance + "px;" )>
    elements()
  </span>
}


@doc "A tabset, takes a list of tuples as argument, e.g. [(\"Tab 1\", \"icon url\", tab1), (\"Tab 2\", \"icon uri\", tab2)] where tab1 and tab2 are controls with no arguments"
control tabset(tabs : Array<Tuple3<String, String, Control>>) {
  var activeTabName : String = tabs.get(0)._1

  <div class="tabbar">
    list((tabName, tabIcon, tabControl) in tabs) {
      <span onclick={ activeTabName = tabName; } class=activeTabName == tabName ? "tab activeTabButton" : "tab inActiveTabButton">
        label(tabName)
      </span>
    }
  </div>
  list((tabName, tabIcon, tabControl) in tabs) {
    <div class=activeTabName == tabName ? "activeTab" : "inActiveTab">
      tabControl()
    </div>
  }
}

// CONTEXT MENU

external control contextMenu()

<javascript>
mobl.ui.generic.contextMenu = function(elements, callback) {
  var root = $("<span>");
  var img = $("<img src='mobl/ui/generic/contextmenu.png' style='float: right;'/>");
  root.append(img);

  img.tap(function(event) {
    event.stopPropagation();
    event.preventDefault();
    var target = img.parent();
    img.hide();
    var item = $("<div class='contextMenu'>");
    item.css('right', "5px");
    item.css('top', target.position().top+6 + "px");
    elements(function(elements, callback) {
      var root5175 = $("<span>");
      callback(root5175); return;
    }, function(node) { item.append(node); });
    target.append(item);
    //item.hide().fadeIn();
    function removeMenu() {
      setTimeout(function() {
        item.remove();
        img.show();
        $("body").unbind('touchstart click', removeMenu);
      }, 500);
    }
    //setTimeout(function() {
    $("body").bind('touchstart click', removeMenu);
    //}, 0);
  });

  callback(root);
};
</javascript>

// Higher level controls

control masterDetail(items : Collection<Dynamic>, masterItem : Control1<Dynamic>, detail : Control1<Dynamic>) {
  group {
    list(it in items) {
      item(onclick={ detailScreen(it, detail); }) {
        masterItem(it)
      }
    }
  }
}

screen detailScreen(it : Dynamic, detail : Control1<Dynamic>) {
  header("Detail") {
    backButton("Back", onclick={ screen return; })
  }
  detail(it)
}

@cond isIpad() && isLandscape()
control masterDetail(items : Collection<Dynamic>, masterItem : Control1<Dynamic>, detail : Control1<Dynamic>) {
  var current : Dynamic = items.one()
  <div width="100%">
    <div style="float:left; width:33%; position:relative; border-right: solid 1px #cccccc;">
      group {
        list(it in items) {
          (it == current ? itemArrow : item)({ current = it; }) {
            masterItem(it)
          }
        }
      }
    </div>
    <div style="float:left; width:66.5%; position:relative; margin-left: 0.5%;">
      detail(current)
    </div>
  </div>
}

// SCROLLERS

external sync function setupScrollers() : void
external sync function scrollUp() : void

control scroller(height : Num = 200) {
  <div class="scroller">
    elements()
  </div>
  script {
    setupScrollers();
  }
}

<javascript>
setTimeout(function() {
  scrollTo(0, -1);
}, 250);

jQuery.fn.tap = function (callback) {
  console.log("New style tap: ");
    if(mobl.isIphone() || mobl.isAndroid() || mobl.isIpad()) {
      new NoClickDelay(this[0], callback);
    } else {
     this.click(callback);
    }
};


mobl.ui.generic.scrollUp = function() {
  scrollTo(0, 0);
}

mobl.ui.generic.setupScrollers = function() {
  setTimeout(function() {
    var allScrollers = $("div.scroller");
    for(var i = 0; i < allScrollers.length; i++) {
      var scroller = allScrollers.eq(i);
      if(!scroller.data("scroller")) {
         scroller.data("scroller", new TouchScroll(scroller[0], {elastic: true}));
      }
    }
  }, 250);
};

setInterval(function() {
  var allScrollers = $("div.scroller");
  for(var i = 0; i < allScrollers.length; i++) {
    var scroller = allScrollers.eq(i).data("scroller");
    if(scroller) {
       scroller.setupScroller();
    }
  }
}, 1000);
</javascript>