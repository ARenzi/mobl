application todo

title "Zodo"
database "zodo"

import mobl
import mobl::ui::ios

entity Task {
  name        : String (searchable)
  description : String (searchable)
  done        : Bool
  date        : DateTime
}

screen addTask() {
  var newTask : Task = Task()

  backButton("Back", onclick={ screen return; })
  basicView("Add") {
    group {
      textField(newTask.name, placeholder="Task name")
    }
    button("Add", onclick={
      add(newTask);
      screen return;
    })
  }
}

screen editTask(t : Task) {
  button("Done", onclick={ screen return; })
  basicView("Edit") {
    group {
      textField(t.name, placeholder="Task name")
      textField(t.description, placeholder="Task description")
    }
    button("Delete", onclick={
      remove(t);
      screen return;
    })
  }
}

screen root() {
  control defaultView(t : Task, editing : Bool) {
    item(onswipe={ editing = true; }) {
      checkBox(t.done) " "
      label(t.name)
    }
  }
  control editView(t : Task, editing : Bool) {
    item(onswipe={ editing = false; }) {
      checkBox(t.done) " "
      label(t.name)
      sideButton("Delete", onclick={ remove(t); })
    }
  }
  button("Add", onclick={ addTask(); })
  var searchQuery = ""
  basicView("Tasks") {
    searchBox(searchQuery)
    group {
      list(t in Task.searchPrefix(searchQuery)) {
        var editing = false
        (editing ? editView : defaultView)(t, editing)
      }
    }
  }
}