application todo

import ui
import model

control taskItem(t : Task) {
  item {
    checkBox(t.done, label=t.name)
    contextMenu {
      button("Delete", onclick={
        remove(t);
      })
      button("Edit", onclick={
        editTask(t);
      })
    }
  }
}

screen addTask() {
  var newTask = Task()

  header("Add") {
    backButton("Back", onclick= { screen return; })
    button("Add", onclick={
      add(newTask);
      screen return;
    })
  }
  group {
    item { textField(newTask.name, placeholder="Task name") }
  }
}

screen editTask(t : Task) {
  header("Edit") {
    button("Done", onclick={ screen return; })
  }
  group {
    item { textField(t.name, placeholder="Task name") }
    item { textField(t.description, placeholder="Task description") }
  }
}

control search() {
  header("Search") {
    backButton()
  }
  var phrase = ""
  searchBox(phrase, placeholder="Search term")
  group {
    list(t in Task.searchPrefix(phrase)) {
      taskItem(t)
    }
  }
}

control tasks() {
  header("Tasks") {
    button("Add", onclick={ addTask(); })
  }
  group {
    list(t in Task.all() order by date desc) {
      taskItem(t)
    }
  }
}

screen root() {
  tabSet([("Tasks", "", tasks), ("Search", "", search)])
}