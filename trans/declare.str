module declare

imports
  include/MoBL
  desugar

strategies

  declare-all =
    with(
      ?Module(_, def*)
      ; <filter(declare)> def*
    )
      
  // Records the definition of an entity in a dynamic rule GetEntity.
  declare:
    Entity(x, super, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules(
          GetEntity : x -> extern
        )

  declare:
    EntityNoSuper(x, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules(
          GetEntity : x -> extern
        )

  declare:
    extern@ExternalType(qid, super, method*) -> <fail>
    with rules (
           GetType : qid -> extern
         )

  declare:
    ExternalTypeNoSuper(qid, method*) -> <fail>
    with extern := <desugar> 
       ; rules (
           GetType : qid -> extern
         )

  declare:
    extern@ExternalGenericType(qid, arg*, method*) -> <fail>
    with arity := <length> arg*
         ; rules (
             GetGenericType : (qid, arity) -> extern
           )


  declare:
    extern@ExternalFunction(qid, farg*, rt) -> <fail>
    with rules (
           GetFunction : qid -> extern 
         )

  declare:
    extern@ExternalSyncFunction(qid, farg*, rt) -> <fail>
    with
      rules (
        GetFunction : qid -> extern
        IsSyncFunction : qid
      )

  declare:
    Function(qid, farg*, rt, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetFunction : qid -> extern 
        )
 
  declare:
    FunctionNoReturnType(qid, farg*, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetFunction : qid -> extern 
        )

  declare:
    SyncFunction(qid, farg*, rt, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetFunction : qid -> extern
          IsSyncFunction : qid
        )

  declare:
    SyncFunctionNoReturnType(qid, farg*, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetFunction : qid -> extern 
          IsSyncFunction : qid
        )
        
  declare:
    TemplateNoRequires(qid, farg*, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetTemplate : qid -> extern 
        )

	declare:
	  Template(qid, farg*, req*, body) -> <fail>
	  with
      extern := <definition-to-external>
	    ; rules (
	        GetTemplate : qid -> extern 
	      )

  declare:
    extern@ExternalTemplate(qid, farg*, req*) -> <fail>
    with rules (
           GetTemplate : qid -> extern 
         )
	
	declare :
	  Screen(qid, farg*, rt, body) -> <fail>
	  with
	    extern := <definition-to-external>
	    ; rules (
	        GetScreen : qid -> extern
	      )

  declare:
    extern@ExternalScreen(qid, farg*, rt) -> <fail>
    with rules (
           GetScreen : qid -> extern 
         )

  declare :
    ScreenNoReturnType(qid, farg*, body) -> <fail>
    with
      extern := <definition-to-external>
      ; rules (
          GetScreen : qid -> extern
        )

strategies // convert to externals
  
  definition-to-external :
    Entity(qid, super, prop*) -> ExternalEntity(qid, super, <alltd(desugar)> prop*)

  definition-to-external :
    EntityNoSuper(qid, prop*) -> ExternalEntity(qid, GenericType(QId("mobl", "Entity"), [SimpleType(qid)]), <alltd(desugar)> prop*)
  
  definition-to-external :
    Function(qid, farg*, rt, stat*) -> ExternalFunction(qid, farg*, rt)
  
  definition-to-external :
    FunctionNoReturnType(qid, farg*, stat*) -> ExternalFunction(qid, farg*, SimpleType(QId("mobl", "void")))

  definition-to-external :
    SyncFunction(qid, farg*, rt, stat*) -> ExternalSyncFunction(qid, farg*, rt)
  
  definition-to-external :
    SyncFunctionNoReturnType(qid, farg*, stat*) -> ExternalSyncFunction(qid, farg*, SimpleType(QId("mobl", "void")))
  
  definition-to-external :
    Template(qid, farg*, req*, elem*) -> ExternalTemplate(qid, farg*, req*)

  definition-to-external :
    TemplateNoRequires(qid, farg*, elem*) -> ExternalTemplate(qid, farg*, [])

  definition-to-external :
    Screen(qid, farg*, rt, _) -> ExternalScreen(qid, farg*, rt)

  definition-to-external :
    ScreenNoReturnType(qid, farg*, _) -> ExternalScreen(qid, farg*, SimpleType(QId("mobl", "void")))

  definition-to-external :
    Import(s) -> Import(s)

  definition-to-external =
    ?c#(_)
    ; where(<string-starts-with(|"External")> c)
    ; try(desugar)