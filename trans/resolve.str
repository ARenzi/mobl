module resolve

imports
  libstratego-lib
  include/MoBL
  desugar
  lookup
  analyze
  type

strategies

  add-tl-namespace :
    Screen(manno*, x, farg*, rt, elem*) -> Screen(manno*, QId(<ModuleName>, x), farg*, rt, elem*)
    where <is-string> x
  
  add-tl-namespace :
    ScreenNoReturnType(manno*, x, farg*, elem*) -> ScreenNoReturnType(manno*, QId(<ModuleName>, x), farg*, elem*)
    where <is-string> x

  add-tl-namespace :
    Entity(manno*, x, super, prop*) -> Entity(manno*, QId(<ModuleName>, x), super, prop*)
    where <is-string> x

  add-tl-namespace :
    EntityNoSuper(manno*, x, prop*) -> EntityNoSuper(manno*, QId(<ModuleName>, x), prop*)
    where <is-string> x
    
  add-tl-namespace :
    ExternalFunction(manno*, x, farg*, rt) -> ExternalFunction(manno*, QId(<ModuleName>, x), farg*, rt)
    where <is-string> x

  add-tl-namespace :
    ExternalSyncFunction(manno*, x, farg*, rt) -> ExternalSyncFunction(manno*, QId(<ModuleName>, x), farg*, rt)
    where <is-string> x

  add-tl-namespace :
    Function(manno*, x, farg*, rt, stat*) -> Function(manno*, QId(<ModuleName>, x), farg*, rt, stat*)
    where <is-string> x

  add-tl-namespace :
    FunctionNoReturnType(manno*, x, farg*, stat*) -> FunctionNoReturnType(manno*, QId(<ModuleName>, x), farg*, stat*)
    where <is-string> x

  add-tl-namespace :
    SyncFunction(manno*, x, farg*, rt, stat*) -> SyncFunction(manno*, QId(<ModuleName>, x), farg*, rt, stat*)
    where <is-string> x

  add-tl-namespace :
    SyncFunctionNoReturnType(manno*, x, farg*, stat*) -> SyncFunctionNoReturnType(manno*, QId(<ModuleName>, x), farg*, stat*)
    where <is-string> x
  
  add-tl-namespace :
    ExternalGenericType(manno*, x, t*, method*) -> ExternalGenericType(manno*, QId(<ModuleName>, x), t*, method*)
    where <is-string> x
    
  add-tl-namespace :
    ExternalType(manno*, x, super, method*) -> ExternalType(manno*, QId(<ModuleName>, x), super, method*)
    where <is-string> x

  add-tl-namespace :
    ExternalTypeNoSuper(manno*, x, method*) -> ExternalTypeNoSuper(manno*, QId(<ModuleName>, x), method*)
    where <is-string> x
    
  add-tl-namespace :
    Template(manno*, x, farg*, elem*) -> Template(manno*, QId(<ModuleName>, x), farg*, elem*)
    where <is-string> x
    
rules
   
  add-namespace :
    SimpleType(qid) -> SimpleType(qid2)
    where not(lookup-type-aux)
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; <lookup-type-aux> SimpleType(<id>); !newid})> [<ModuleName>|<bagof-Imports>]

  // Add namespace to possible type references which look like vars
  add-namespace :
    Var(qid) -> Var(qid2)
    where <is-string> qid
    where not(<lookup-type-aux> SimpleType(qid))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; <lookup-type-aux> SimpleType(<id>); !newid})> [<ModuleName>|<bagof-Imports>]

  add-namespace :
    GenericType(qid, t*) -> GenericType(qid2, t2*)
    where not(lookup-type-aux)
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; <lookup-type-aux> GenericType(<id>, t*); !newid})> [<ModuleName>|<bagof-Imports>]
        ; t2* := <alltd(add-namespace)> t*
    
  add-namespace :
    TemplateCall(qid, arg*, elem*) -> TemplateCall(qid2, arg2*, elem2*)
    where not(<lookup-template-aux> (qid, arg*))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-template-aux; !newid})> [<ModuleName>|<bagof-Imports>]
        ; arg2*  := <alltd(add-namespace)> arg*
        ; elem2* := <alltd(add-namespace)> elem*

  add-namespace :
    TemplateCallNoBody(qid, arg*) -> TemplateCallNoBody(qid2, arg2*)
    where not(<lookup-template-aux> (qid, arg*))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-template-aux; !newid})> [<ModuleName>|<bagof-Imports>]
        ; arg2*  := <alltd(add-namespace)> arg*

  add-namespace :
    TemplateCallNoArgs(qid, elem*) -> TemplateCallNoArgs(qid2, elem2*)
    where not(<lookup-template-aux> (qid, []))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-template-aux; !newid})> [<ModuleName>|<bagof-Imports>]
        ; elem2* := <alltd(add-namespace)> elem*

  add-namespace :
    TemplateCallNoArgsNoBody(qid) -> TemplateCallNoArgsNoBody(qid2)
    where not(<lookup-template-aux> (qid, []))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-template-aux; !newid})> [<ModuleName>|<bagof-Imports>]

  add-namespace :
    Call(qid, e*) -> Call(qid2, e2*)
    where arg-types := <map(type-of)> e*
        ; not(<lookup-function-aux> (qid, arg-types))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-function-aux; !newid})> [<ModuleName>|<bagof-Imports>]
        ; e2* := <alltd(add-namespace)> e*

  add-namespace :
    Call(qid, e*) -> Call(qid2, e2*)
    where arg-types := <map(type-of)> e*
        ; not(<lookup-screen-aux> (qid, arg-types))
        ; [qid2] := <filter({c, newid: ?c; <prefix-qid(|c)> qid; ?newid; lookup-screen-aux; !newid})> [<ModuleName>|<bagof-Imports>]
        ; e2* := <alltd(add-namespace)> e*

  prefix-qid(|qid) :
    qid2 -> QId(qid, qid2)
    where is-string

  prefix-qid(|qid) :
    QId(q, x) -> QId(<prefix-qid(|qid)> q, x)
