module rename

imports
  include/MoBL

strategies
  
  rename-all = alltd(rename)
  
  rename-var : 
   (x, t) -> y
   with y := x{<newname> x}
      ; rules(
           RenameId : x -> y
           TypeOf   : y -> t
        )

  rename-local-function : 
   name -> name2
   where "template" := <RenameScope>
   with name2 := name{<newname> name}
      ; rules(
           RenameFunction : name -> name2
        )
        
  rename-local-function :
    name -> name
    where not("template" := <RenameScope>)
  
  rename-ref-var :
   (x, t) -> y
   with y := <rename-var>
      ; rules(
           IsRefVar : y
        )
  
  is-ref :
    y -> <IsRefVar> y
  
  is-ref :
    Var(y) -> <IsRefVar> y
  
  rename :
    Var(x) -> Var(<RenameId> x)
    
  rename :
    Call(x, e*) -> Call(<RenameFunction> x, <rename-all> e*)
    
  rename-scope(s) =
    {| RenameId, RenameFunction
     : s
     |}

  template-scope(s) =
    {| RenameScope
     : rules ( RenameScope := "template" )
     ; rename-scope(s)
     |}

  action-scope(s) =
    {| RenameScope
     : rules ( RenameScope := "action" )
     ; rename-scope(s)
     |}
  
  rename :
    ExternalGenericType(qid, x*, method*) -> ExternalGenericType(qid, x*, method2*)
    with {| RenameType
          : <map(\ x -> x where rules ( RenameType : SimpleType(x) -> SimpleType(x){"TemplateType"} ) \)> x*
          ; method2* := <rename-all> method*
          |}

  rename = RenameType

  rename :
    Template(name, farg*, req*, body) -> Template(name, farg2*, req*, body2)
    with template-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-ref-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

	rename :
	  TemplateNoRequires(name, arg*, body) -> TemplateNoRequires(name, arg2*, body2)
	  with template-scope( 
	         arg2* := <map(\ FArg(x, t) -> FArg(<rename-ref-var> (x, t), t) \)> arg*
	         ; body2 := <rename-all> body
	       )

  rename :
    Screen(name, arg*, rt, body) -> Screen(name, arg2*, rt, body2)
    with template-scope( 
           arg2* := <map(\ FArg(x, t) -> (x, t) \; rename-ref-var)> arg*
           ; body2 := <rename-all> body
         )

  rename :
    ScreenNoReturn(name, farg*, body) -> ScreenNoReturn(name, farg2*, body2)
    with template-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-ref-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

strategies
  
  rename :
    Function(qid, farg*, rt, body) -> Function(qid2, farg2*, rt, body2)
    with action-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> qid

  rename :
    FunctionNoReturnType(qid, farg*, body) -> FunctionNoReturnType(qid2, farg2*, body2)
    with action-scope( 
           farg2* := <map(\ FArg(x, t) ->FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> qid

  rename :
    SyncFunction(qid, farg*, rt, body) -> SyncFunction(qid2, farg2*, rt, body2)
    with action-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> qid

  rename :
    SyncFunctionNoReturnType(qid, farg*, body) -> SyncFunctionNoReturnType(qid2, farg2*, body2)
    with action-scope( 
           farg2* := <map(\ FArg(x, t) ->FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> qid
  
  rename :
    Init(body) -> Init(body2)
    with action-scope(body2 := <rename-all> body)

strategies
  
  rename :
    VarDecl(name, t, e) -> VarDecl(<rename-var> (name, t), t, <rename-all> e)
    where "action" := <RenameScope>

  rename :
    VarDecl(name, t, e) -> VarDecl(<rename-ref-var> (name, t), t, <rename-all> e)
    where "template" := <RenameScope>
   
  rename :
    List(x, t, e, body) -> List(x2, t, e2, body2)
    with rename-scope(
           x2 := <rename-var> (x, t)
           ; e2 := <rename-all> e
           ; body2 := <rename-all> body
         )