module rename

imports
  include/MoBL

strategies
  
  rename-all = alltd(rename)
  
  rename-var : 
   (x, t) -> y
   with y := x{<newname> x};
        rules(
           RenameId : x -> y
           TypeOf   : y -> t 
        )
  
  rename :
    Var(x) -> Var(y)
    where y := <RenameId> x

  rename-scope(s) =
    {| RenameId
     : s
     |}

  /*rename :
    Init(t) -> Init(<rename-scope(rename-all)> t)*/

  rename :
    Template(name, farg*, req*, body) -> Template(name, farg2*, req*, body2)
    with rename-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

	rename :
	  TemplateNoRequires(name, arg*, body) -> TemplateNoRequires(name, arg2*, body2)
	  with rename-scope( 
	         arg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> arg*
	         ; body2 := <rename-all> body
	       )

  rename :
    Screen(name, arg*, rt, body) -> Screen(name, arg2*, rt, body2)
    with rename-scope( 
           arg2* := <map(\ FArg(x, t) -> (x, t) \; rename-var)> arg*
           ; body2 := <rename-all> body
         )

  rename :
    ScreenNoReturn(name, farg*, body) -> ScreenNoReturn(name, farg2*, body2)
    with rename-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

  rename :
    Function(qid, farg*, rt, body) -> Function(qid, farg2*, rt, body2)
    with rename-scope( 
           farg2* := <map(\ FArg(x, t) -> FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

  rename :
    FunctionNoReturnType(qid, farg*, body) -> FunctionNoReturnType(qid, farg2*, body2)
    with rename-scope( 
           farg2* := <map(\ FArg(x, t) ->FArg(<rename-var> (x, t), t) \)> farg*
           ; body2 := <rename-all> body
         )

  rename :
    VarDecl(name, t, e) -> VarDecl(<rename-var> (name, t), t, <rename-all> e)
  
  rename :
    ScreenVarDecl(name, t, e) -> ScreenVarDecl(<rename-var> (name, t), t, <rename-all> e)
    
  rename :
    List(x, t, e, body) -> List(x2, t, e2, body2)
    with rename-scope(
           x2 := <rename-var> (x, t)
           ; e2 := <rename-all> e
           ; body2 := <rename-all> body
         )