module rename

imports
  include/MoBL
  declare

strategies
  
  rename-all = alltd(rename)
  
  rename-var : 
   (x, t) -> y
   with y := x{<newname> x}
      ; rules(
           RenameId : x -> y
           TypeOf   : y -> t
        )

  rename-local-function : 
   c#([name|rest]) -> name2
   where "template" := <RenameScope>
   with name2 := name{<newname> name}
      ; rules(
           RenameFunction : name -> name2
        )
      ; <try(declare)> c#([name2|rest])
        
  rename-local-function :
    c#([name|rest]) -> name
    where not("template" := <RenameScope>)
  
  rename-ref-var :
   (x, t) -> y
   with y := <rename-var>
      ; rules(
           IsRefVar : y
        )
  
  is-ref :
    y -> <IsRefVar> y
  
  is-ref :
    Var(y) -> <IsRefVar> y
  
  rename :
    Var(x) -> Var(<RenameId> x)

  rename :
    Assignment(x, e) -> Assignment(<RenameId> x, <rename-all> e)
    
  rename :
    Call(x, e*) -> Call(<RenameFunction> x, <rename-all> e*)
    
  rename-scope(s) =
    {| RenameId, RenameFunction
     : s
     |}

  template-scope(s) =
    {| RenameScope
     : rules ( RenameScope := "template" )
     ; rename-scope(s)
     |}

  action-scope(s) =
    {| RenameScope
     : rules ( RenameScope := "action" )
     ; rename-scope(s)
     |}
  
  rename :
    ExternalGenericType(qid, x*, method*) -> ExternalGenericType(qid, x*, method2*)
    with {| RenameType
          : <map(\ x -> x where rules ( RenameType : SimpleType(x) -> SimpleType(x){"TemplateType"} ) \)> x*
          ; method2* := <rename-all> method*
          |}

  rename = RenameType

  rename :
    Template(name, farg*, body) -> Template(name, farg2*, body2)
    with template-scope( 
           farg2* := <map(rename-ref-farg)> farg*
           ; body2 := <rename-all> body
         )

  rename-ref-farg :
    FArg(x, t) -> FArg(<rename-ref-var> (x, t), t)    

  rename-ref-farg :
    FArgOptional(x, t, e) -> FArgOptional(<rename-ref-var> (x, t), t, <rename-all> e)    

  rename-farg :
    FArg(x, t) -> FArg(<rename-var> (x, t), t)    

  rename-farg :
    FArgOptional(x, t, e) -> FArg(<rename-var> (x, t), t)    

  rename :
    Screen(name, arg*, rt, body) -> Screen(name, arg2*, rt, body2)
    with template-scope( 
           arg2* := <map(rename-ref-farg)> arg*
           ; body2 := <rename-all> body
         )

  rename :
    ScreenNoReturnType(name, farg*, body) -> ScreenNoReturnType(name, farg2*, body2)
    with template-scope( 
           farg2* := <map(rename-ref-farg)> farg*
           ; body2 := <rename-all> body
         )

strategies
  
  rename :
    f@Function(qid, farg*, rt, body) -> Function(qid2, farg2*, rt, body2)
    with action-scope( 
           farg2* := <map(rename-farg)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> f

  rename :
    f@FunctionNoReturnType(qid, farg*, body) -> FunctionNoReturnType(qid2, farg2*, body2)
    with action-scope( 
           farg2* := <map(rename-farg)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> f

  rename :
    f@SyncFunction(qid, farg*, rt, body) -> SyncFunction(qid2, farg2*, rt, body2)
    with action-scope( 
           farg2* := <map(rename-farg)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> f

  rename :
    f@SyncFunctionNoReturnType(qid, farg*, body) -> SyncFunctionNoReturnType(qid2, farg2*, body2)
    with action-scope( 
           farg2* := <map(rename-farg)> farg*
           ; body2 := <rename-all> body
         )
         ; qid2 := <rename-local-function> f
  
  rename :
    Init(body) -> Init(body2)
    with action-scope(body2 := <rename-all> body)

  rename :
    Callback(body) -> Callback(body2)
    with action-scope(body2 := <rename-all> body)

strategies
  
  rename :
    VarDecl(name, t, e) -> VarDecl(<rename-var> (name, t), t, <rename-all> e)
    where "action" := <RenameScope>

  rename :
    VarDecl(name, t, e) -> VarDecl(<rename-ref-var> (name, t), t, <rename-all> e)
    where "template" := <RenameScope>
   
  rename :
    List(x, t, e, body) -> List(x2, t, e2, body2)
    with rename-scope(
           x2 := <rename-var> (x, t)
           ; e2 := <rename-all> e
           ; body2 := <rename-all> body
         )