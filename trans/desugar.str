module desugar

imports
  include/MoBL
  libstratego-lib
  
strategies

  desugar-all :
    Module(qid, def*) -> Module(qid, def2*)
    with {| ModuleName
          : rules ( ModuleName := qid )
          ; def2* := <innermost(desugar)> def*
          |}
  
  desugar :
    FunctionNoReturnType(name, farg*, stat*) -> Function(name, farg*, SimpleType("void"), stat*)

  desugar :
    SyncFunctionNoReturnType(name, farg*, stat*) -> SyncFunction(name, farg*, SimpleType("void"), stat*)
  
  desugar :
    IfNoElse(e, stat*) -> If(e, stat*, [])
    
  desugar :
    HtmlShort(tag, attr*) -> Html(tag, attr*, [], tag)
  
  desugar :
    EntityNoSuper(qid, prop*) -> Entity(qid, GenericType(QId("mobl", "Entity"), [SimpleType(qid)]), prop*)

  desugar :
    ExternalTypeNoSuper(qid, method*) -> ExternalType(qid, SimpleType(QId("mobl", "Object")), method*)
    
  desugar :
    PropertyNoAnnos(x, t) -> Property(x, t, [])
  
  desugar :
    ScreenNoReturn(name, farg*, body) -> Screen(name, farg*, SimpleType("void"), body)
  
  desugar :
    TemplateNoRequires(qid, farg*, elem*) -> Template(qid, farg*, [], elem*)

  desugar :
    TemplateCallNoArgs(qid, prop*, body) -> TemplateCall(qid, [], prop*, body)
    
  desugar :
    TemplateCallNoArgsNoBody(qid, prop*) -> TemplateCall(qid, [], prop*, [])
    
  desugar :
    TemplateCallNoArgsNoProperties(qid, body) -> TemplateCall(qid, [], [], body)
    
  desugar :
    TemplateCallNoArgsNoPropertiesNoBody(qid) -> TemplateCall(qid, [], [], [])
    
  desugar :
    TemplateCallNoBody(qid, arg*, prop*) -> TemplateCall(qid, arg*, prop*, [])
  
  desugar :
    TemplateCallNoProperties(qid, arg*, body) -> TemplateCall(qid, arg*, [], body)
  
  desugar :
    TemplateCallNoPropertiesNoBody(qid, arg*) -> TemplateCall(qid, arg*, [], [])