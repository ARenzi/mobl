module desugar

imports
  include/MoBL
  libstratego-lib
  
strategies

  desugar-all :
    Module(qid, def*) -> Module(qid, def2*)
    with {| ModuleName
          : rules ( ModuleName := qid )
          ; def2* := <innermost(desugar)> def*
          |}

  desugar-all :
    Application(qid, set*, def*) -> Application(qid, set*, def2*)
    with {| ModuleName
          : rules ( ModuleName := qid )
          ; def2* := <innermost(desugar)> def*
          |}
  
  desugar :
    FunctionNoReturnType(name, farg*, stat*) -> Function(name, farg*, SimpleType("void"), stat*)

  desugar :
    SyncFunctionNoReturnType(name, farg*, stat*) -> SyncFunction(name, farg*, SimpleType("void"), stat*)

  desugar :
    IfNoElse(e, stat*) -> If(e, stat*, [])
    
  desugar :
    HtmlShort(tag, attr*) -> Html(tag, attr*, [], tag)
  
  desugar :
    EntityNoSuper(qid, prop*) -> Entity(qid, GenericType(QId("mobl", "Entity"), [SimpleType(qid)]), prop*)

  desugar :
    ExternalTypeNoSuper(qid, method*) -> ExternalType(qid, SimpleType(QId("mobl", "Object")), method*)
    
  desugar :
    PropertyNoAnnos(x, t) -> Property(x, t, [])
  
  desugar :
    ScreenNoReturnType(name, farg*, body) -> Screen(name, farg*, SimpleType("void"), body)
  
  desugar = normalize
  
  normalize :
    TemplateCallNoArgs(qid, body) -> TemplateCall(qid, [], body)
    
  normalize :
    TemplateCallNoArgsNoBody(qid) -> TemplateCall(qid, [], [])
    
  normalize :
    TemplateCallNoBody(qid, arg*) -> TemplateCall(qid, arg*, [])
  
  normalize :
    EntityNoSuper(qid, prop*) -> Entity(qid, GenericType(QId("mobl", "Entity"), [SimpleType(qid)]), prop*)
  
