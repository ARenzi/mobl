module generation/ui

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/MoBL
  lib/editor-common
  desugar
  generation/emit
  generation/action
  generation/lift
  generation/cps-action

rules

  definition-to-js :
    Template(qid, farg*, req*, elems) -> <emit> $[
      mobiworks.rootScope.setLocal('[<qid-to-html> qid]', function([fargs]) {
        var [root] = $("<span>");
        [body] 
      });
      ]
    with {| InTemplate
		      : rules ( InTemplate := 1 )
		      ; root := <newname> "root"
		      ; body-elem := <elems-to-js-callback(|root)> elems
		      ; fargs :=  <map(?FArg(<id>, _)); <concat> [["scope"], <id>, ["callback"]]; separate-by(!", "); concat-strings> farg*
		      ; init-stats := <filter(?Init(<id>)); concat> elems
		      ; if not([] := init-stats) then
		          init-fn := <newname> "init"
		          ; body := <concat; cps-statements> [<cps-lift-expressions; debug> init-stats, [body-elem]]
		        else
		          body := body-elem
		        end
		      |}

  definition-to-js :
    Screen(qid, farg*, rt, elem*) -> <definition-to-js> Template(qid, farg*, SimpleType(QId("mobl", "void")), elem*)

  elems-to-js-callback(|root) =
    <concat; elems-to-js(|root)> [<id>, [$[callback([root]);]]]

  elems-to-js(|root) :
    [TemplateCall(qid, arg*, prop*, elems)|rest*] -> 
    $[var [subScope] = new mobiworks.LinkedMap(scope);
      [body-stats]
      [templatecall]
]
    with elem-root := <newname> "root"
       ; subScope := <newname> "subScope"
       ; if not([] := elems) then
           body-stats := $[[subScope].setLocal('elements', function(scope, callback) {
                             var [elem-root] = $("<span>");
                             [<elems-to-js-callback(|elem-root)> elems]
                           });
]
         else
           body-stats := ""
         end
       ; if not([] := prop*) then
           props := <map(prop-to-bind)> prop*
           ; prop-set := $[
               var children = node.children();
               for(var i = 0; i < children.length; i++) {
                  var c = children.eq(i);
                  [props]
               }]
         else
           prop-set := ""
         end
       ; args  := <map(ref-expression-to-js); <concat> [[subScope], <id>, [
           $[function(node) {
               [prop-set]
               [root].append(node.contents());
               [<elems-to-js(|root)> rest*]
             }]
         ]]; separate-by(!", "); concat-strings> arg*
       ; templatecall := <concat-strings> ["(scope.get('", <qid-to-html> qid, "'))(", args, ");"]
  
  prop-to-bind :
    PropertyArg(x, e) -> $[c.bind('[x]', function() { [<expression-to-js> e]; });]
  
  elems-to-js(|root) :
    [Html(tag, arg*, elems, _)|rest*] -> $[
      var [node] = $("<[tag] [<escape> args]>");
      [<filter(databind-tag-arg-to-js(|tag, node)); concat-strings> arg*]
      [body]
    ]
    with args := <filter(tag-arg-to-html); separate-by(!" "); concat-strings> arg*
       ; node := <newname> "node"
       ; body := <elems-to-js(|node)> <concat> [elems, [
           $[[root].append([node]);
           [<elems-to-js(|root)> rest*]]]]

  elems-to-js(|root) :
    [s|rest*] -> $[[s][<elems-to-js(|root)> rest*]]
    where <is-string> s
    
  elems-to-js(|root) :
    [Init(_)|rest*] -> <elems-to-js(|root)> rest*
  
  elems-to-js(|root) :
    [] -> ""
  
  tag-arg-to-html :
    HtmlArg(name, s) -> $[[name]=[s]]

  databind-tag-arg-to-js(|tag, node) :
    HtmlDatabindArg(e) -> $[
      var [r] = [es];
      [node].[method]([r].get());
      [r].addSetListener(function(val) {
        [node].[method](val);
      });
    ]
    with es := <ref-expression-to-js> e
       ; r  := <newname> "ref"
       ; if "input" := tag then
           method := "val"
         else
           method := "text"
         end
    
  elems-to-js(|root) :
    [List(x, t, e, elems)|rest*] -> $[
      var [node] = $("<ul>");
      var [list] = [<expression-to-js> e];
      [root].append([node]);
      var [rest] = function() {
        [<elems-to-js(|root)> rest*]
      };
      var [renderList] = function(isRerender) {
        [node].empty();
	      [list].list(null, function(results) {
	        results = results.reverse();
	        var [oldScope] = scope;
	        function renderOne() {
              var scope = new mobiworks.LinkedMap([oldScope]);
	            scope.setLocal('[x]', results.pop());
	            [<concat; elems-to-js(|node)> [elems, [$[
	              if(results.length > 0) {
	                renderOne();
	              } else if(!isRerender) {
	                [rest]();
	              }
	            ]]]]          
	        }
	        if(results.length > 0) {
	          renderOne();
	        } else if(!isRerender) {
	          [rest]();
	        }
	      });
      };
      [list].addEventListener($[$'add', 'remove'$]$, function() { [renderList](true); });
      [renderList](false);
    ]
    with node := <newname> "node"
       ; rest := <newname> "rest"
       ; i := <newname> "i"
       ; renderList := <newname> "renderList"
       ; list := <newname> "list"
       ; oldScope := <newname> "oldScope"
    
  elems-to-js(|root) :
    [ScreenString(s)|rest*] -> $[
      [root].append([s]);
      [<elems-to-js(|root)> rest*]]
    
  /*ref-expression-to-html :
    e -> $["[<ref-expression-to-js; html-escape> e]"]

  expression-to-html :
    e -> $["[<expression-to-js; html-escape> e]"]
  
  html-escape =
    string-replace(|"\"", "&quot;")*/

rules // utils
	
  qid-to-html :
    s -> s
    where is-string

  qid-to-html :
    QId(qid, x) -> $[[qids]__[x]]
    with qids := <qid-to-html> qid	