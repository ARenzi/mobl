module generation/cps-action

imports
  include/MoBL
  generation/lift
  generation/action
  generation/ui
  declare
  type
  rename
  lookup

  //cps-statements = debug(!"statements: "); fail
strategies
  
  cps-statements :
    [Return(e)|stat*] -> <cps-expression(|[$[if(callback) callback(result);
                                             return;
]|stat*])> e

  cps-statements :
    [ScreenReturn(e)|stat*] -> <cps-expression(|[$[if(screenCallback) screenCallback(result);
                                                   return;
]|stat*])> e

  cps-statements :
    [Return()|stat*] -> <cps-statements> [$[if(callback) callback();
                                            return;
]|stat*]

  cps-statements :
    [ScreenReturn()|stat*] -> <cps-statements> [$[if(screenCallback) screenCallback();
                                                  return;
]|stat*]

  cps-statements :
    [VarDecl(x, t, e)|stat*] -> <cps-expression(|[$[var [x] = result;
]|stat*])> e
    where not(InTemplate)

  cps-statements :
    [VarDeclInferred(x, e)|stat*] -> <cps-expression(|[$[var [x] = result;
]|stat*])> e
    where not(InTemplate)

  cps-statements :
    [VarDecl(x, t, e)|stat*] -> <cps-expression(|[$[var [x] = result;
]|stat*])> e
    where InTemplate
        ; not(<is-ref> x)

  cps-statements :
    [VarDecl(x, t, e)|stat*] -> <cps-expression(|[$[var [x] = ref(result);
]|stat*])> e
    where InTemplate
        ; <is-ref> x

  cps-statements :
    [VarDeclInferred(x, e)|stat*] -> <cps-expression(|[$[var [x] = result;
]|stat*])> e
    where InTemplate
        ; not(<is-ref> x)

  cps-statements :
    [VarDeclInferred(x, e)|stat*] -> <cps-expression(|[$[var [x] = ref(result);
]|stat*])> e
    where InTemplate
        ; <is-ref> x

  cps-statements :
    [Assignment(qid, e)|stat*] -> <cps-expression(|[$[[<qid-to-js> qid] = result;
]|stat*])> e
    where not(<is-ref> qid)

  cps-statements :
    [Assignment(qid, e)|stat*] -> <cps-expression(|[$[[<qid-to-js> qid].set(result);
]|stat*])> e
    where <is-ref> qid

  cps-statements :
    [Async(stats)|stat*] -> 
      $[[<cps-statements> stats]
        [<cps-statements> stat*]
]

  cps-statements :
    [ExpStat(e)|stat*] -> <cps-expression(|stat*)> e
  
  cps-statements :
    [If(e, true-stat*, false-stat*)|stat*] -> 
      <cps-expression(|[
        $[if(result) {
            [<cps-statements> [true-stat*, stat*]]
          } else {
            [<cps-statements> [false-stat*, stat*]]
          }
]])> e

  cps-statements :
    [For(x, _, e, stats)|stat*] -> 
      <cps-expression(|[
        $[[<expression-to-js> e].list(null, function([coll]) {
            [coll] = [coll].reverse();
            function [processOne]() {
              var [x] = [coll].pop();
              [<concat; cps-statements> [stats, [$[
                if([coll].length > 0) [processOne](); else [rest]();
              ]]]]
            }
            function [rest]() {
              [<cps-statements> stat*]
            }
            if([coll].length > 0) [processOne](); else [rest]();
          });
]])> e
    with coll := <newname> "coll"
       ; processOne := <newname> "processOne"
       ; rest := <newname> "rest"

 
  cps-statements :
    [s|stat*] -> <concat-strings> [s, <cps-statements> stat*]
    where <is-string> s
  
  cps-statements :
    [] -> ""

rules
  
  cps-expression(|stat*) =
    ?e
    ; if is-sync then
        !$[var result = [<expression-to-js> e];
           [<cps-statements> stat*]]
      else
        cps-exp(|stat*)
      end
  
  cps-exp(|stat*) :
    BinMethodCall(e1, op, e2) -> 
      <cps-expression(|[$[var [result] = result;
], <cps-expression(|[
        $[op('[op]', [result], result, function(result) {
            [<cps-statements> stat*]
          });
]])> e2])> e1
    with result := <newname> "result"


  cps-exp(|stat*) :
    e@CollectionCons(ct, t, e*) -> $[var result = [<expression-to-js> e];
                         [<cps-statements> stat*]]

  cps-exp(|stat*) :
    e@EntityCons(_, _) -> $[var result = [<expression-to-js> e];
                         [<cps-statements> stat*]]

  cps-exp(|stat*) :
    Call(qid, e*) -> <concat-strings> [<qid-to-js> qid, "(", args, ");"]
    where not(<IsSyncFunction> qid)
        ; not(<GetScreen> qid)
    with args := <arguments-to-js; <concat> [<id>, [
$[function(result) {
    [<cps-statements> stat*]
  }]]]; separate-by(!", "); concat-strings> e*

  cps-exp(|stat*) :
    Call(qid, e*) -> <concat-strings> ["mobl.call('", <qid-to-js> qid, "', [", args, "], ", $[function(result) {
    [<cps-statements> stat*]
  }], ");"]
    where <GetScreen> qid
    with args := <ref-arguments-to-js; separate-by(!", "); concat-strings> e*

  cps-exp(|stat*) :
    e@Call(qid, e*) -> $[var result = [<expression-to-js> e];
                         [<cps-statements> stat*]]
    where <IsSyncFunction> qid

  cps-exp(|stat*) :
    MethodCall(e, x, e*) -> <concat-strings> [<expression-to-js> e, ".", x, "(", args, ");"]
    //where not(<IsSyncFunction> qid)
    with args := <map(expression-to-js); <concat> [<id>, [
$[function(result) {
    [<cps-statements> stat*]
  }]]]; separate-by(!", "); concat-strings> e*
  