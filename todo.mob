application todo

title "Zodo"
database "zodo"

import mobl
   
entity Task {
  name        : String
  description : Text
  done        : Bool
}

screen addTask() {
  var newTask : Task = Task{}
    
  backButton("Back", onclick={ screen return; })
  basicView("Add") {
    inputs {
      inputString(newTask.name, placeholder="Task name")
    }
    button("Add", onclick={ add(newTask); screen return; })
  }
}

screen editTask(t : Task) {
  topRightButton("Done", onclick={ screen return; })
  basicView("Edit") {
    inputs {
      inputString(t.name, placeholder="Task name")
      inputString(t.description, placeholder="Task description")
    }
    button("Delete", onclick={ remove(t); screen return; })
  }
}  

screen root() { 
  topRightButton("Add", onclick={ addTask(); }) 
  basicView("Tasks") {
    ul {
	    list(t : Task in Task.all().order("done", false).order("name", true).limit(10)) {  
	      listitem(onclick={ editTask(t); }) {
	        inputCheckbox(t.done, onclick={}) " "
	        outputString(t.name)
	        deleteButton("Delete", onclick={ remove(t); })
	      }
	    }
    }
  }
}