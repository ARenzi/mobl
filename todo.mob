application todo

title "Zodo"
database "zodo"

import mobl
   
entity Task {
  name        : String
  description : Text
  done        : Bool
}

template basicView(header : String) {
  header(header) 
  elements()
}

screen addTask() {
  var newTask : Task = Task{}
    
  basicView("Add") {
    backButton("Back", onclick={ screen return; })
    inputs {
      inputString(newTask.name, placeholder="Task name")
    }
    button("Add", onclick={ add(newTask); screen return; })
  }
}

screen editTask(t : Task) {
  basicView("Edit") {
    topRightButton("Done", onclick={ screen return; })
    inputs {
      inputString(t.name, placeholder="Task name")
      inputString(t.description, placeholder="Task description")
    }
    button("Delete", onclick={ remove(t); screen return; })
  }
}  

screen root() { 
  basicView("Tasks") {
    topRightButton("Add", onclick={ addTask(); }) 

    list(t : Task in Task.all().order("done", false).order("name", true).limit(10)) {  
      listitem(onclick={ editTask(t); }) {
        inputCheckbox(t.done, onclick={}) " "
        outputString(t.name)
        deleteButton("Delete", onclick={ remove(t); })
      }
    }
    button("Ask for my name", onclick={ alert("Hello there " + promptString("What is your name?"));})
  }
}