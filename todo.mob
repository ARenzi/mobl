application todo

title "Zodo"
database "zodo"

import mobl
   
entity Task {
  name        : String
  description : Text
  done        : Bool
}  

template basicView(header : String) {
  header(header) 
  elements()
}

screen addTask() : Task {
  var newTask : Task := Task{}
    
  function add() { 
    screen return newTask;
  } 
  
  function cancel() {
    screen return null; 
  } 
 
  basicView("Add") {
    backButton("Back")[click:=cancel()]
    inputs {
      inputString(newTask.name)
    }
    button("Add")[click:=add()]
  }
}

screen editTask(t : Task) {
  function done() { 
    screen return;
  } 
 
  basicView("Edit") {
    topRightButton("Done")[click:=done()]
    inputs {
      inputString(t.name)
      inputString(t.description)
    }
  }
}  
 
screen root() { 
  function addScreen() {
    var newTask : Task := addTask();
    if(newTask != null) {
      add(newTask);
      flush();
    }
  }
  function edit(t : Task) {
    editTask(t);
    flush();
  }

  function removeTask(t : Task) {
    remove(t); 
    flush();
  }
	
  basicView("Tasks") {
    topRightButton("Add")[click:=addScreen()]

    list(t : Task in Task.all().order("done", false).order("name", true).limit(10)) {  
      listitem[click:=edit(t)] {
        inputCheckbox(t.done)[change:=flush()] " "
        outputString(t.name)
        deleteButton("Delete")[click:=removeTask(t)] 
      }
    }
  }
}