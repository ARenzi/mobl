module todo
 
import mobl
   
entity Task {
  name        : String
  done        : Bool
}  

template standardLayout(header : String) {
  header(header) 
  elements()
} 

screen addTask() : Task {
  var newTask : Task := Task{}
    
  function add() { 
    screen return newTask;
  } 
  
  function cancel() {
    screen return null; 
  } 
 
  standardLayout("Add task") {
    backButton("Back")[click:=cancel()]
    ul {
      listitem { inputString(newTask.name) }
    }
    button("Add")[click:=add()]
  }
}  
 
screen root() { 
  function add() {
    var newTask : Task := addTask();
    if(newTask != null) {
      add(newTask);
      flush();
    }
  }

  function removeTask(t : Task) {
    remove(t); 
    flush();
  }
	
  standardLayout("Tasks") {
    topRightButton("Add")[click:=add()]

    list(t : Task in Task.all().order("done", false).order("name", true).limit(10)) {  
      listitem {
        inputCheckbox(t.done)[change:=flush()] " "
        outputString(t.name)
        deleteButton("Delete")[click:=removeTask(t)] 
      }
    }
  }
}