application todo

title "Zodo"
database "zodo"

import mobl
   
entity Task {
  name        : String
  description : Text
  done        : Bool
}

template basicView(header : String) {
  header(header) 
  elements()
}

screen addTask() {
  var newTask : Task := Task{}
    
  function add() {
    add(newTask);
    flush(); 
    screen return;
  } 
  
  function cancel() {
    screen return; 
  } 
 
  basicView("Add") {
    backButton("Back")[click:=cancel()]
    inputs {
      inputString(newTask.name)
    }
    button("Add")[click:=add()]
  }
}

screen editTask(t : Task) {
  function done() { 
    screen return;
  }
 
  basicView("Edit") {
    topRightButton("Done")[click:=done()]
    inputs {
      inputString(t.name)
      inputString(t.description)
    }
  }
}  

screen root() { 
  basicView("Tasks") {
    topRightButton("Add", onclick:={ addTask(); }) 

    list(t : Task in Task.all().order("done", false).order("name", true).limit(10)) {  
      listitem(onclick:={ editTask(t); flush(); }) {
        inputCheckbox(t.done, onclick:={ flush(); }) " "
        outputString(t.name)
        deleteButton("Delete", onclick:={ remove(t); flush(); })
      }
    }
    button("Ask for my name", onclick:={ alert("Hello there " + promptString("What is your name?"));})
  }
}