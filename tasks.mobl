application tasks

resource taskicon.png

import mobl::ui::generic

entity Task {
  name    : String (searchable)
  done    : Bool
  dueDate : DateTime
}

screen root() {
  header("Todo") {
    button("Add", onclick={
      addTask();
    })
    button("Whatever", onclick={
      var t = Task(name="Whatever");
      add(t);
    })
  }
  var phrase = ""
  searchBox(phrase)
  group {
    list(task in Task.searchPrefix(phrase) where done == false) {
      item {
        checkBox(task.done, label=task.name)
        contextMenu() {
          button("Remove", onclick={
            remove(task);
          })
        }
      }
    }
  }
}

screen addTask() {
  var newTask = Task(done = false, dueDate=now())
  header("Add") {
    backButton("Back", onclick={ screen return; })
  }
  group {
    item { textField(newTask.name, placeholder="Task name") }
  }
  button("Add", onclick={
    add(newTask);
    screen return;
  })
}